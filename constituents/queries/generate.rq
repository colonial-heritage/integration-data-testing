###############################
# Query for generating a search graph from constituent descriptions in dataset 'constituents.ttl' from the Data Registry
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?this a cc:Person ; # For BC; should become cc:Actor as soon the SPARQL queries in the frontend have been updated
    cc:name ?name ;
    cc:birthPlace ?birthPlaceName ;
    cc:birthDate ?birthDate ;
    cc:birthYear ?birthYear ;
    cc:deathPlace ?deathPlaceName ;
    cc:deathDate ?deathDate ;
    cc:deathYear ?deathYear ;
    cc:publisher_en ?publisherNameEn ;
    cc:publisher_nl ?publisherNameNl .
}
WHERE {
  BIND(?_iri AS ?this)

  ####################
  # Name
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E33_E41_Linguistic_Appellation ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404670> ; # Name
      crm:P190_has_symbolic_content ?name
    ] .
  }

  ####################
  # Birth place
  ####################

  OPTIONAL {
    ?this crm:P98i_was_born/crm:P7_took_place_at/crm:P1_is_identified_by/crm:P190_has_symbolic_content ?birthPlaceName .
  }

  ####################
  # Birth date (earliest known)
  ####################

  OPTIONAL {
    ?this crm:P98i_was_born/crm:P4_has_time-span/crm:P82a_begin_of_the_begin ?birthDate .

    # Values can have an xsd date type but not be a valid date (!) - ignore these
    FILTER(DATATYPE(?birthDate) = xsd:date || DATATYPE(?birthDate) = xsd:gYear || DATATYPE(?birthDate) = xsd:gYearMonth)
    FILTER(COALESCE(xsd:date(STR(?birthDate)), '!') != '!')
  }

  ####################
  # Birth year
  ####################

  BIND(IF(BOUND(?birthDate), YEAR(?birthDate), ?null) AS ?birthYear)

  ####################
  # Death place
  ####################

  OPTIONAL {
    ?this crm:P100i_died_in/crm:P7_took_place_at/crm:P1_is_identified_by/crm:P190_has_symbolic_content ?deathPlaceName .
  }

  ####################
  # Death date (earliest known)
  ####################

  OPTIONAL {
    ?this crm:P100i_died_in/crm:P4_has_time-span/crm:P82a_begin_of_the_begin ?deathDate .

    # Values can have an xsd date type but not be a valid date (!) - ignore these
    FILTER(DATATYPE(?deathDate) = xsd:date || DATATYPE(?deathDate) = xsd:gYear || DATATYPE(?deathDate) = xsd:gYearMonth)
    FILTER(COALESCE(xsd:date(STR(?deathDate)), '!') != '!')
  }

  ####################
  # Death year
  ####################

  BIND(IF(BOUND(?deathDate), YEAR(?deathDate), ?null) AS ?deathYear)

  ####################
  # Part of dataset
  ####################

  ?this la:member_of ?dataset .

  OPTIONAL {
    ?dataset schema:name ?datasetName
    FILTER(LANG(?datasetName) = "" || LANGMATCHES(LANG(?datasetName), "en"))
  }

  ####################
  # Publisher of dataset
  ####################

  OPTIONAL {
    ?dataset schema:publisher/schema:name ?publisherName

    BIND(IF(LANG(?publisherName) = "en", ?publisherName, ?null) AS ?publisherNameEn)
    BIND(IF(LANG(?publisherName) = "nl", ?publisherName, ?null) AS ?publisherNameNl)
  }
}
