###############################
# Query for generating a search graph from object descriptions in dataset 'objects.ttl' from the Data Registry
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skolem: <https://triplydb.com/.well-known/genid/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?this a cc:HeritageObject ;
    cc:additionalType ?typeName ; # For BC; remove as soon as locale-aware names are in use
    cc:additionalType_en ?typeNameEn ;
    cc:additionalType_nl ?typeNameNl ;
    cc:about ?subjectName ; # For BC; remove as soon as locale-aware names are in use
    cc:about_en ?subjectNameEn ;
    cc:about_nl ?subjectNameNl ;
    cc:identifier ?identifier ;
    cc:name ?name ;
    cc:description ?description ;
    cc:inscription ?inscription ;
    cc:material ?materialName ; # For BC; remove as soon as locale-aware names are in use
    cc:material_en ?materialNameEn ;
    cc:material_nl ?materialNameNl ;
    cc:technique ?techniqueName ; # For BC; remove as soon as locale-aware names are in use
    cc:technique_en ?techniqueNameEn ;
    cc:technique_nl ?techniqueNameNl ;
    cc:creator ?creatorName ;
    cc:owner ?ownerName ; # For BC; remove as soon as locale-aware names are in use
    cc:owner_en ?ownerNameEn ;
    cc:owner_nl ?ownerNameNl ;
    cc:yearCreatedStart ?beginOfTheBeginYear ;
    cc:yearCreatedEnd ?endOfTheEndYear ;
    cc:locationCreated ?locationCreatedName ; # Only for indexing
    cc:countryCreated_en ?countryCreatedNameEn ;
    cc:countryCreated_nl ?countryCreatedNameNl ;
    cc:alternateName ?alternateLocationCreatedName, ?alternateCountryCreatedName ; # Only for indexing
    cc:publisher_en ?publisherNameEn ;
    cc:publisher_nl ?publisherNameNl .
}
WHERE {
  BIND(?_iri AS ?this)

  ####################
  # Type
  ####################

  OPTIONAL {
    ?this crm:P2_has_type/rdfs:label ?typeName
    FILTER(LANG(?typeName) = "en") # For BC; remove as soon as locale-aware names are in use

    # BIND(IF(LANG(?typeName) = "en", ?typeName, ?null) AS ?typeNameEn)
    # BIND(IF(LANG(?typeName) = "nl", ?typeName, ?null) AS ?typeNameNl)
  }

  ####################
  # Identifier
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E42_Identifier ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404626> ; # Identification number
      crm:P190_has_symbolic_content ?identifier
    ] .
  }

  ####################
  # Name
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E33_E41_Linguistic_Appellation ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404670> ; # Name
      crm:P190_has_symbolic_content ?name
    ] .
  }

  ####################
  # Description
  ####################

  OPTIONAL {
    ?this crm:P67i_is_referred_to_by [
      crm:P2_has_type <http://vocab.getty.edu/aat/300435416> ; # Description
      crm:P190_has_symbolic_content ?description
    ] .
  }

  ####################
  # Subject
  ####################

  OPTIONAL {
    ?this crm:P62_depicts/rdfs:label ?subjectName
    FILTER(LANG(?subjectName) = "en") # For BC; remove as soon as locale-aware names are in use

    # BIND(IF(LANG(?subjectName) = "en", ?subjectName, ?null) AS ?subjectNameEn)
    # BIND(IF(LANG(?subjectName) = "nl", ?subjectName, ?null) AS ?subjectNameNl)
  }

  ####################
  # Material
  ####################

  OPTIONAL {
    ?this crm:P45_consists_of/rdfs:label ?materialName
    FILTER(LANG(?materialName) = "en") # For BC; remove as soon as locale-aware names are in use

    # BIND(IF(LANG(?materialName) = "en", ?materialName, ?null) AS ?materialNameEn)
    # BIND(IF(LANG(?materialName) = "nl", ?materialName, ?null) AS ?materialNameNl)
  }

  ####################
  # Inscription
  ####################

  OPTIONAL {
    ?this crm:P128_carries [
      crm:P2_has_type <http://vocab.getty.edu/aat/300028702> ; # Inscription
      crm:P190_has_symbolic_content ?inscription ;
    ] .
  }

  ####################
  # Technique
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P32_used_general_technique/rdfs:label ?techniqueName
    FILTER(LANG(?techniqueName) = "en") # For BC; remove as soon as locale-aware names are in use

    # BIND(IF(LANG(?techniqueName) = "en", ?techniqueName, ?null) AS ?techniqueNameEn)
    # BIND(IF(LANG(?techniqueName) = "nl", ?techniqueName, ?null) AS ?techniqueNameNl)
  }

  ####################
  # Owner
  ####################

  OPTIONAL {
    ?this crm:P52_has_current_owner/schema:name ?ownerName
    FILTER(LANG(?ownerName) = "en") # For BC; remove as soon as locale-aware names are in use

    # BIND(IF(LANG(?ownerName) = "en", ?ownerName, ?null) AS ?ownerNameEn)
    # BIND(IF(LANG(?ownerName) = "nl", ?ownerName, ?null) AS ?ownerNameNl)
  }

  ####################
  # Creator
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P14_carried_out_by/rdfs:label ?creatorName
  }

  ####################
  # Date of creation
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82a_begin_of_the_begin ?beginOfTheBegin .

    # Values can have an xsd date type but not be a valid date (!) - ignore these
    FILTER(DATATYPE(?beginOfTheBegin) = xsd:date || DATATYPE(?beginOfTheBegin) = xsd:gYear || DATATYPE(?beginOfTheBegin) = xsd:gYearMonth)
    FILTER(COALESCE(xsd:date(STR(?beginOfTheBegin)), '!') != '!')
  }

  BIND(IF(BOUND(?beginOfTheBegin), YEAR(?beginOfTheBegin), ?null) AS ?beginOfTheBeginYear)

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82b_end_of_the_end ?endOfTheEnd .

    # Values can have an xsd date type but not be a valid date (!) - ignore these
    FILTER(DATATYPE(?endOfTheEnd) = xsd:date || DATATYPE(?endOfTheEnd) = xsd:gYear || DATATYPE(?endOfTheEnd) = xsd:gYearMonth)
    FILTER(COALESCE(xsd:date(STR(?endOfTheEnd)), '!') != '!')
  }

  BIND(IF(BOUND(?endOfTheEnd), YEAR(?endOfTheEnd), ?null) AS ?endOfTheEndYear)

  ####################
  # Location of creation
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P7_took_place_at ?location .
    ?location gn:name ?unofficialLocationCreatedName ;
      gn:featureCode ?featureCode .

    # Official name may not exist, see e.g. https://sws.geonames.org/3827414/
    OPTIONAL {
      ?location gn:officialName ?officialLocationCreatedName
      FILTER(LANG(?officialLocationCreatedName) IN ("en", "nl"))
    }

    BIND(IF(BOUND(?officialLocationCreatedName), ?officialLocationCreatedName, ?unofficialLocationCreatedName) AS ?locationCreatedName)

    OPTIONAL {
      ?location gn:alternateName ?alternateLocationCreatedName
      FILTER(LANG(?alternateLocationCreatedName) IN ("en", "nl"))
    }

    # Country of which the location is a part
    OPTIONAL {
      ?location gn:parentCountry ?country .
      ?country gn:name ?unofficialCountryCreatedName .

      # Official name may not exist
      OPTIONAL {
        ?country gn:officialName ?officialCountryCreatedName
        FILTER(LANG(?officialCountryCreatedName) IN ("en", "nl"))
      }

      BIND(IF(BOUND(?officialCountryCreatedName), ?officialCountryCreatedName, ?unofficialCountryCreatedName) AS ?countryCreatedName)

      OPTIONAL {
        ?country gn:alternateName ?alternateCountryCreatedName
        FILTER(LANG(?alternateCountryCreatedName) IN ("en", "nl"))
      }
    }

    # Location could be a country already
    # https://www.geonames.org/export/codes.html
    BIND(IF(?featureCode = <https://www.geonames.org/ontology#A.PCLI>, ?locationCreatedName, ?countryCreatedName) AS ?countryCreated)
    BIND(IF(LANG(?countryCreated) = "en", ?countryCreated, ?null) AS ?countryCreatedNameEn)
    BIND(IF(LANG(?countryCreated) = "nl", ?countryCreated, ?null) AS ?countryCreatedNameNl)
  }

  ####################
  # Part of dataset
  ####################

  ?this la:member_of ?dataset .

  ####################
  # Publisher of dataset
  ####################

  OPTIONAL {
    ?dataset schema:publisher/schema:name ?publisherName

    BIND(IF(LANG(?publisherName) = "en", ?publisherName, ?null) AS ?publisherNameEn)
    BIND(IF(LANG(?publisherName) = "nl", ?publisherName, ?null) AS ?publisherNameNl)
  }
}
